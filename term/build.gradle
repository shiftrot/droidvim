apply plugin: 'com.android.application'
repositories {
    maven { url "https://jitpack.io" }
}

import java.text.SimpleDateFormat
android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        versionName = "1.0.0"
        versionCode 1

        buildConfigField "String", "WHATS_NEW", '""'
        buildConfigField "String", "BASE64_PUBLIC_KEY", String.format('"%s"', file('base64-public-key.txt').getText().replaceAll("[\r\n].*", ""))

        externalNativeBuild {
            ndkBuild {
                arguments '-C', 'src/main', '-j', Runtime.runtime.availableProcessors() + ''
                abiFilters 'armeabi', 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
            }
        }

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets {
        origin {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    buildTypes {
        release {
            shrinkResources = true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        flavorDimensions "default"
        origin {
            dimension "default"
            versionName = new SimpleDateFormat("yyyy.M.dd.HH.mm.ss").format(new Date()) + ".dev"
        }
        droidvim {
            dimension "default"
            setApplicationId("com.droidvim")
        }
    }
}

dependencies {
    implementation('androidx.appcompat:appcompat:1.0.0-beta01')
    implementation('androidx.annotation:annotation:1.0.0-beta01')
    implementation('androidx.legacy:legacy-support-v4:1.0.0-beta01')
    implementation project(':emulatorview')
    implementation project(':libtermexec')
    implementation 'com.github.hedzr:android-file-chooser:1.1.11'
}

